plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14' apply false
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {

    ext {
        /*
         * Versions should be equal in each sub-project
         */
        versions = [
                'ada'  : System.getenv("BUILD_NUMBER") ?: '0.0.42',
                'akka' : '2.5.11',
                'scala': '2.12'
        ]

        /*
         * Dependency Management
         */
        libs = [
                'akka-actor'             : "com.typesafe.akka:akka-actor_${versions['scala']}:${versions['akka']}",
                'akka-actor-typed'       : "com.typesafe.akka:akka-actor-typed_${versions['scala']}:${versions['akka']}",
                'akka-streams'           : "com.typesafe.akka:akka-stream_${versions['scala']}:${versions['akka']}",
                'assertj-core'           : 'org.assertj:assertj-core:3.9.1',
                'easymock'               : 'org.easymock:easymock:4.0.1',
                'jackson-annotations'    : 'com.fasterxml.jackson.core:jackson-annotations:2.9.7',
                'jackson-databind'       : 'com.fasterxml.jackson.core:jackson-databind:2.9.7',
                'jackson-dataformat-yaml': 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.7',
                'jackson-datatype-guava' : 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.9.7',
                'jackson-datatype-jdk8'  : 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.7',
                'jackson-datatype-sr310' : 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.7',
                'reactive-streams'       : 'org.reactivestreams:reactive-streams:1.0.2',
                'wiremock'               : 'com.github.tomakehurst:wiremock:2.19.0'
        ]
    }

    /*
     * We only have Java modules
     */
    apply plugin: 'java'
    apply plugin: 'io.franzbecker.gradle-lombok'

    /*
     * Common properties
     */
    group 'com.ibm.ada'
    version versions['ada']

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    /*
     * Common dependencies
     */
    dependencies {
        compile 'com.google.guava:guava:27.0-jre'
        compile 'org.slf4j:slf4j-api:1.7.25'

        testCompile 'junit:junit:4.12'
    }

}
